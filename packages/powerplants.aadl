package powerplants
public
	
with engine;
with dataDictionary;
with ARP4761, EMV2;

system powerplants
	features
		engine1_generatorDC : out data port dataDictionary::DC28VoltRaw;
		engine2_generatorDC_1 : out data port dataDictionary::DC28VoltRaw;
		engine2_generatorDC_2 : out data port dataDictionary::DC28VoltRaw;
		engine3_generatorDC : out data port dataDictionary::DC28VoltRaw;
end powerplants;

system implementation powerplants.impl
	subcomponents
		engine1 : system engine::engine.item;
		engine2 : system engine::engine.item;
		engine3 : system engine::engine.item;
	connections
		new_connection: port engine1.generatorDC -> engine1_generatorDC;
		new_connection2: port engine2.generatorDC -> engine2_generatorDC_1;
		new_connection3: port engine2.generatorDC -> engine2_generatorDC_2;
		new_connection4: port engine3.generatorDC -> engine3_generatorDC;
		
	annex EMV2 {**
		use types S18_ErrorLibrary;
		use behavior S18_ErrorLibrary::failureWithSeverityClassification;
		composite error behavior
			states
				-- All engines failed
				[ engine1.failed and engine2.failed and engine3.failed]->HazardousFailure;	
				
				-- Two engines failed
				[(engine1.failed and engine2.failed) or
				 	(engine1.failed and engine3.failed) or 
					(engine2.failed and engine3.failed)]-> MajorFailure;
			
				-- One engine failed
				[engine1.failed or engine2.failed or engine3.failed]->MinorFailure;
		end composite;
		
		properties
			-- The all engine out condition description will be pickup in
			--     the FTA report, and as the next higher system failed
			--     in the FHA report 
			EMV2::severity => ARP4761::Hazardous applies to HazardousFailure;
			ARP4761::hazards => 
			([
				description => "All engine out condition";
				environment => "All";
				failure => "All engine out with loss of services";
				failureEffect => "Loss of thrust";
				phases => ("Takeoff, In-flight, Landing");
				comment => "All engine inoperative with possible progression to forced landing";
			])
			applies to HazardousFailure;
		
			EMV2::severity => ARP4761::Major applies to MajorFailure;
			ARP4761::hazards => 
			([
				description => "Two enigne out condition";
				environment => "All";
				failure => "Two engines out only partial services available";
				failureEffect => "Loss of thrust";
				phases => ("Takeoff, In-flight, Landing");
				comment => "Two engine inoperative with failure to maintain altitude";
			])
			applies to MajorFailure;
			
			EMV2::severity => ARP4761::Minor applies to MinorFailure;
			ARP4761::hazards => 					
			([
				description => "One enigne out condition";
				environment => "All";
				failure => "One engines out";
				failureEffect => "Loss of thrust";
				phases => ("Takeoff, In-flight, Landing");
				comment => "One engine inoperative";
			])
			applies to MinorFailure;
			
			
	**};
end powerplants.impl;

end powerplants;